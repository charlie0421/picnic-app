plugins {
    id "com.android.application"
    id "kotlin-android"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "dev.flutter.flutter-gradle-plugin"
}

apply plugin: 'com.google.gms.google-services'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode') ?: '1'
def flutterVersionName = localProperties.getProperty('flutter.versionName') ?: '1.0'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "io.iconcasting.picnic.app"
    compileSdk flutter.compileSdkVersion
    ndkVersion = "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        apiVersion = '1.7'
        languageVersion = '1.7'
        freeCompilerArgs += [
            "-Xskip-metadata-version-check"
        ]
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "io.iconcasting.picnic.app"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        manifestPlaceholders += [
            'sentryEnableCompose': 'false'
        ]
    }

    signingConfigs {
        release {
            if (System.getenv()["CI"]) {
                storeFile file(System.getenv()["CM_KEYSTORE_PATH"])
                storePassword System.getenv()["CM_KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["CM_KEY_ALIAS"]
                keyPassword System.getenv()["CM_KEY_PASSWORD"]
            } else {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig System.getenv()["CI"] ? signingConfigs.release : signingConfigs.debug
            minifyEnabled false
            shrinkResources false
        }
    }

    bundle {
        storeArchive {
            enable = true
        }
    }

}

flutter {
    source '../..'
}

// Flutter가 기대하는 위치로 AAB 파일 복사하는 태스크
task copyAabToFlutterBuild {
    description 'Flutter가 기대하는 위치로 AAB 파일 복사'
    
    doLast {
        // 프로젝트 루트에서 상대 경로로 안전하게 처리
        def projectRoot = project.rootProject.projectDir.parentFile
        def flutterBuildDir = new File(projectRoot, 'build/app/outputs/bundle')
        
        println "프로젝트 루트: ${projectRoot.absolutePath}"
        println "Flutter 빌드 디렉토리: ${flutterBuildDir.absolutePath}"
        
        // Debug AAB 복사
        def debugSource = new File(project.buildDir, 'outputs/bundle/debug')
        if (debugSource.exists()) {
            def debugTarget = new File(flutterBuildDir, 'debug')
            debugTarget.mkdirs()
            
            debugSource.listFiles().findAll { it.name.endsWith('.aab') }.each { aabFile ->
                def targetFile = new File(debugTarget, aabFile.name)
                targetFile.bytes = aabFile.bytes
                println "✓ Debug 복사 완료: ${aabFile.name} -> ${targetFile.absolutePath}"
            }
        }
        
        // Release AAB 복사
        def releaseSource = new File(project.buildDir, 'outputs/bundle/release')  
        if (releaseSource.exists()) {
            def releaseTarget = new File(flutterBuildDir, 'release')
            releaseTarget.mkdirs()
            
            releaseSource.listFiles().findAll { it.name.endsWith('.aab') }.each { aabFile ->
                def targetFile = new File(releaseTarget, aabFile.name)
                targetFile.bytes = aabFile.bytes
                println "✓ Release 복사 완료: ${aabFile.name} -> ${targetFile.absolutePath}"
            }
        }
    }
}

// 모든 bundle 태스크 완료 후 자동으로 복사
afterEvaluate {
    tasks.matching { it.name.startsWith('bundle') }.configureEach {
        finalizedBy copyAabToFlutterBuild
    }
}

repositories {
    // ✅ CDN 미러를 최우선으로
    maven { url "https://maven.aliyun.com/repository/public" }
    maven { url "https://artifact.bytedance.com/repository/pangle" }
    maven { url "https://artifact.bytedance.com/repository/pangle-global" }
    maven { url "https://jitpack.io" }
    google()
    mavenCentral() // fallback
    // jcenter() ❌ 제거
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.tapjoy:tapjoy-android-sdk:14.3.1'
    implementation "androidx.concurrent:concurrent-futures:1.1.0"
    implementation files('libs/pincrux_offerwall.aar')

    // Pangle SDK
    implementation 'com.pangle.global:ads-sdk:6.5.0.5'
    implementation 'com.pangle.global:crash_monitor:1.3.8-rc.1'
    implementation 'com.pangle.global:gecko_pgl:1.0.0.1'

    // Flutter 의존성은 Flutter Gradle 플러그인이 자동 처리
}